plugins {
    id "groovy"
}

ext {
    curriculumFileName = "glossary"

    // if you only have one single language, remove either DE or EN. If you want support for other
    // translations, make sure to add them here.
    languages = ["DE", "EN"]

    // you can define several additional tags that will each be used to create a separate resulting
    // html and pdf file. The following example will build files without the REMARKS suffix (regular files)
    // and those with the REMARKS-suffix. It will then also render content which is enclosed in
    // ifeval::["{suffix}" == "REMARKS"] .... endif::[]
    //
    // suffixTags = ["REMARKS"]
    suffixTags = [""]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('org.apache.groovy:groovy-bom:4.0.24')
    implementation 'org.apache.groovy:groovy'
    implementation 'org.apache.groovy:groovy-json'

    testImplementation platform('org.spockframework:spock-bom:2.3-groovy-4.0')
    testImplementation 'org.spockframework:spock-core'
}

tasks.register('createFiles') {
    dependsOn 'generateTranslationTables', 'sortTermsAlphabetically', ':gradle-tools:buildDocs'
}

// Ensure buildDocs runs AFTER the preparation tasks
gradle.projectsEvaluated {
    project(':gradle-tools').tasks.named('buildDocs') {
        mustRunAfter(':generateTranslationTables', ':sortTermsAlphabetically')
    }
}

tasks.register('generateTranslationTables', JavaExec) {
    dependsOn tasks.compileGroovy
    jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED',
               '--add-opens', 'java.base/java.io=ALL-UNNAMED',
               '--add-opens', 'java.base/java.util=ALL-UNNAMED']
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'Translator'
}

apply from: 'scripts/sortTermsAlphabetically.gradle'
apply from: 'scripts/generateTermTables.gradle'

defaultTasks "createFiles"
